{
id: "white_swaying_flower",
prototype: ["stationary_vulnerable"],
solid_dimensions: ["enemy","common"],
editor_info: {
	category: "enemies, forest",
},
hitpoints: 200,
solid_area: null,
properties: {
#-------------------------- constructor/destructor logic --------------------------#
	specific_creature_type_constructor: "commands :: [
														create_segments,
														add_object(_head),
														set(_head.upside_down, upside_down),
													]",


#-------------------------- metadata --------------------------#

	attack_damage: "1",

	_segments: { type: "[obj white_swaying_flower.segment]", default: [] },
	_head: { type: "obj white_swaying_flower.head", init: "object('white_swaying_flower.head',x,y,{facing: facing, parent: me})" },
	_segments_and_head: "_segments + [_head]",

	create_segments: "[set(_segments, new_set_of_segments), schedule(1, bind_command(def() map(_segments, [value.attach_to_spawner, add_object(value), set(value.time_in_animation,8*index)] )))]
			where new_set_of_segments = map(range(7), object('white_swaying_flower.segment',midpoint_x,midpoint_y,{facing: facing, parent: me}))",

	position_segments: "map(_segments_and_head,  [
								set(value.relative_x, new_pos.x),
								set(value.relative_y, ((6 + new_pos.y) * upside_down)),
								set(value.rotate, offset.x * (-upside_down))
							]
							where new_pos = rotated_segment_position(offset.x, offset.y) 
							where offset = individual_segment_offset(index)
						)",
	
	vertical_spacing: "16 + 14* wobble(sin(cycle * 2.25))",
	
	wobble: "def(decimal input_data) -> decimal sign(input_data) * lib.math.ease_in_quint(abs(input_data)) ",

						
	individual_segment_offset: "def(int s_index) -> {x: decimal, y: decimal}
							{	
								x: (6.0 * sin(cycle*5.5 + spacing * s_index) * fraction),
								y: (vertical_spacing * s_index)
							}
								where spacing = decimal :: ( 360 / size(_segments_and_head) )
								where fraction = decimal :: lib.math.ease_out_back(decimal(s_index) /(size(_segments_and_head)-1))",
								
	rotated_segment_position: "def( decimal angle, decimal distance ) -> {x: decimal, y: decimal}
									{
										x: distance * cos(_angle),
										y: distance * sin(_angle)
									} where _angle = angle + 90",
								
},

on_process: "position_segments",

animation: [
	{
		id: "normal",
		no_remove_alpha_borders: true,
		image: "enemies/white-shooting-flower.png",
		attack_area: "all",
		body_area: "all",
		accel_y: 0,
		rect: [76,5,120,49],
	},
],

object_type: [{
	id: "segment",
	prototype: ["hittable_attache"],
	zsub_order: 1,
	on_end_anim: "animation(animation)",
	animation: [
		{
			image: "enemies/white-shooting-flower.png",
			no_remove_alpha_borders: true,
			id: "normal",
			duration: 6,
			rect: [6,5,24,23],
			frames: 5,
			reverse: true,
			frames_per_row: 1,
			body_area: "all",
			attack_area: "all",
			pad: 3,
		},
	],
},
{
	id: "head",
	prototype: ["hittable_attache"],
	zsub_order: 2,
	on_end_anim: "animation(animation)",
	timer_frequency: 80,
	on_timer: "[sound_falloff('pollen-shot.wav'),spawn('white_swaying_flower.shot', mid_x, mid_y + 30*upside_down, {facing:facing, velocity_x:1d100-50, velocity_y:200*upside_down })]",
	animation: [
		{
			image: "enemies/white-shooting-flower.png",
			no_remove_alpha_borders: true,
			id: "normal",
			duration: 4,
			rect: [28,5,72,49],
			frames: 3,
			reverse: true,
			frames_per_row: 1,
			body_area: "all",
			attack_area: "all",
			pad: 3,
		},
	],
},
{
	id: "shot",
	always_active: true,
	object_level_collisions: true,
	zorder: "@include data/zorder.cfg:near_player_foreground_effects",
	prototype: ["shot"],
	on_end_anim: "animation(animation)",
	
	properties: {
		death_effects: "def(string type) -> commands [
			spawn('forest_shooting_plant_wallmounted.flash', {
				x: mid_x,
				y: mid_y,
			}),
		]",			

		contrail_particles: {
			type: "obj legacy_particle_attache",
			init: "object('legacy_particle_attache', mid_x, mid_y, {
				_particle_type: 'white_forest_flower_small_trail',
				parent: me,
				time_to_die: 50,
				variable_name: 'contrail_particles',
			})",
			persistent: false,
		},
	},
	
	animation: {
		image: "enemies/white-shooting-flower.png",
		id: "normal",
		duration: 2,
		pad: 3,
		rect: [124,5,132,13],
		frames: 3,
		body_area: "all",
		attack_area: "all",
		accel_y: 4,
	},

	on_create: "[
		sound_falloff('white-swaying-flower-shot.wav'),
		schedule(300,force_death()),
		spawn('forest_shooting_plant_wallmounted.flash', {
			x: mid_x,
			y: mid_y,
		}),


		add_object(contrail_particles),

		spawn('effects_attache_glow_source', mid_x, mid_y, {
			time_to_die: 0,
			parent: me,
			flicker_type: enum window_light,
			graphics: enum soft_glow_small,
			scale_factor: 0.65
		}),
	]",

},
],

}

